# Copyright (c) Huawei Technologies Co., Ltd. 2021. All rights reserved.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../base/config.gni")

config("shared_memory_config") {
  include_dirs = [
    "include",
    "../../base/include",
    "${OHOS_PROFILER_DIR}/interfaces/innerkits",
    "//third_party/bounds_checking_function/include",
    "//commonlibrary/c_utils/base/include",
  ]
}

ohos_source_set("shared_memory_source") {
  sources = [
    "src/share_memory_allocator.cpp",
    "src/share_memory_block.cpp",
  ]
  public_configs = [
    ":shared_memory_config",
    "${OHOS_PROFILER_DIR}/device/base:hiprofiler_test_config",
  ]

  public_deps = [
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf_lite",
    "//third_party/bounds_checking_function:libsec_static",
  ]

  if (current_toolchain != host_toolchain) {
    defines = [ "HAVE_HILOG" ]
    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
    ]
  }
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
}

ohos_shared_library("shared_memory") {
  deps = [ ":shared_memory_source" ]
  install_enable = true
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
}

ohos_static_library("shared_memory_lite") {
  sources = [
    "src/share_memory_allocator.cpp",
    "src/share_memory_block.cpp",
  ]
  public_configs = [
    ":shared_memory_config",
    "${OHOS_PROFILER_DIR}/device/base:hiprofiler_test_config",
  ]

  defines = [ "NO_PROTOBUF" ]

  if (current_toolchain != host_toolchain) {
    defines += [ "HAVE_HILOG" ]
    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
    ]
  }

  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
}
