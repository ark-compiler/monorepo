# Copyright (c) Huawei Technologies Co., Ltd. 2021. All rights reserved.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../protos.gni")

hiperf_data_protos_defines = [ "./hiperf_plugin_config.proto" ]

#######################################################
proto_out_dir = "$root_gen_dir/cpp/" + rebase_path(".", "//")
proto_rel_out_dir = rebase_path(proto_out_dir, root_build_dir)

#######################################################
hiperf_data_protos_codegen = []
hiperf_data_protos_codegen_standard = []
hiperf_data_protos_codegen_all = []
foreach(proto, hiperf_data_protos_defines) {
  name = get_path_info(proto, "name")
  hiperf_data_protos_codegen += [
    "$proto_out_dir/$name.pb.h",
    "$proto_out_dir/$name.pb.cc",
  ]
  hiperf_data_protos_codegen_standard += [
    "$proto_out_dir/${name}_standard.pb.h",
    "$proto_out_dir/${name}_standard.pb.cc",
  ]
}
hiperf_data_protos_codegen_all += hiperf_data_protos_codegen
hiperf_data_protos_codegen_all += hiperf_data_protos_codegen_standard

action("hiperf_data_protos_protoc") {
  script = "${OHOS_PROFILER_DIR}/build/protoc.sh"
  sources = hiperf_data_protos_defines
  outputs = hiperf_data_protos_codegen_all
  args = [
    "$libc_dir_proto",
    "$root_output_dir_proto",
    "$proto_rel_out_dir",
    "--cpp_out",
    "$proto_rel_out_dir",
    "--proto_path",
    rebase_path(".", root_build_dir),
  ]
  args += rebase_path(sources, root_build_dir)
  deps = [ "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protoc(${host_toolchain})" ]
}

config("hiperf_data_protos_config") {
  include_dirs = [ "$proto_out_dir" ]
}

ohos_source_set("hiperf_data_cpp") {
  deps = [ ":hiperf_data_protos_protoc" ]
  public_deps = [
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf",
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf_lite",
  ]
  include_dirs = [ "$proto_out_dir" ]
  public_configs = [ ":hiperf_data_protos_config" ]
  sources = hiperf_data_protos_codegen
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
}

ohos_source_set("hiperf_data_cpp_standard") {
  deps = [ ":hiperf_data_protos_protoc" ]
  public_deps = [
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf",
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf_lite",
  ]
  include_dirs = [ "$proto_out_dir" ]
  public_configs = [ ":hiperf_data_protos_config" ]
  sources = hiperf_data_protos_codegen_standard
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
}
