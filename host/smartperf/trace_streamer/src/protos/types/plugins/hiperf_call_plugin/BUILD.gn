# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../protos.gni")

hiperf_call_plugin_protos_defines = [ "./hiperf_call_plugin_config.proto" ]

#######################################################
proto_out_dir = "$root_gen_dir/cpp/" + rebase_path(".", "//")
proto_rel_out_dir = rebase_path(proto_out_dir, root_build_dir)
print("proto_out_dir", proto_out_dir, proto_rel_out_dir)

#######################################################
hiperf_call_plugin_protos_codegen = []
foreach(proto, hiperf_call_plugin_protos_defines) {
  name = get_path_info(proto, "name")
  hiperf_call_plugin_protos_codegen += [
    "$proto_out_dir/$name.pb.h",
    "$proto_out_dir/$name.pb.cc",
  ]
}

action("ts_hiperf_call_plugin_protos_protoc") {
  script = "${OHOS_TRACE_STREAMER_DIR_PROTOC}"
  sources = hiperf_call_plugin_protos_defines
  outputs = hiperf_call_plugin_protos_codegen
  args = [
    "$libc_dir_proto",
    "$root_output_dir_proto",
    "--cpp_out",
    "$proto_rel_out_dir",
    "--proto_path",
    rebase_path(".", root_build_dir),
  ]
  args += rebase_path(sources, root_build_dir)
}

config("hiperf_call_plugin_protos_config") {
  include_dirs = [ "$proto_out_dir" ]
}

source_set("ts_hiperf_call_plugin_protos_cpp") {
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
  deps = [ ":ts_hiperf_call_plugin_protos_protoc" ]
  public_deps = [
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf_lite_static",
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf_static",
  ]
  include_dirs = [ "$proto_out_dir" ]
  public_configs = [ ":hiperf_call_plugin_protos_config" ]
  sources = hiperf_call_plugin_protos_codegen
}
