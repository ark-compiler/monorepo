# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_runtime/test/test_helper.gni")
import("./automatedcases/typeinfer_failed_cases.gni")

declare_args() {
  is_enable_typeinfer_automatedcases = false
}

if (is_enable_typeinfer_automatedcases) {
  ts_files = exec_script(rebase_path("./automatedcases/find_ts_files.py"),
                         [
                           "automatedcases",
                           rebase_path("./automatedcases"),
                         ],
                         "list lines")

  ts_files -= failed_list

  foreach(file, ts_files) {
    host_typeinfer_test_action(file) {
      deps = []
      is_enable_builtins_dts = true
    }
  }
}

group("ark_typeinfer_test") {
  testonly = true

  test_list = [
    "add2dyn",
    "assertTypeRules",
    "binarysearch",
    "bitwise_op",
    "builtins",
    "callarg0dyn",
    "callarg1dyn",
    "callargs2dyn",
    "callargs3dyn",
    "callirangedyn",
    "callithisrange",
    "callprimitivebuiltin",
    "class",
    "classwithproperty",
    "createemptyarray",
    "createemptyobject",
    "createobjecthavingmethod",
    "defineasyncfunc",
    "defineclasswithbuffer",
    "definefunctiondyn",
    "definegeneratorfunc",
    "div2dyn",
    "getiterator",
    "infergetterfunc",
    "interface",
    "ldboolean",
    "ldglobalvar",
    "ldinfinity",
    "ldnan",
    "ldnull",
    "ldobjbyindex",
    "ldobjbyname",
    "ldobjbyvalue",
    "ldsuperproperty",
    "ldundefined",
    "lex_env_var",
    "lex_env_var_class",
    "lex_env_var_duplication",
    "lex_env_var_multiple",
    "logic_op",
    "loopbeginphi",
    "mod2dyn",
    "mul2dyn",
    "newobjdynrange",
    "parainfer",
    "parainfer_class",
    "phiwithloop",
    "phiwithnumber",
    "stconsttoglobalrecord",
    "stglobalvar",
    "stlettoglobalrecord",
    "sub2dyn",
    "supercall",
    "superclass",
    "throwdyn_1",
    "throwdyn_2",
    "tryldglobalbyname",
    "trystglobalbyname",
    "tsarraytobuiltin",
    "typeof",
    "variable_after_loops",
  ]

  deps = []
  foreach(test, test_list) {
    deps += [ "${test}:${test}Es2abcAotTypeInferAction" ]
  }

  deps += [ "module_test:ark_typeinfer_module_test" ]
  deps += [ "es2abc_feature:ark_typeinfer_es2abc_feature_test" ]

  if (is_enable_typeinfer_automatedcases) {
    foreach(file, ts_files) {
      deps += [ ":${file}Es2abcAotTypeInferAction" ]
    }
  }
}
